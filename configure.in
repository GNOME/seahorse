dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)

AC_INIT(seahorse, 0.6.1)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AM_PROG_LIBTOOL
AM_GCONF_SOURCE_2

PKG_CHECK_MODULES(SEAHORSE, libgnomeui-2.0 libglade-2.0)

dnl **********
dnl GPGME
dnl **********
dnl The line below only checks for versions >= 0.3.14, so it doesn't 
dnl is valid for us. Cloning part og gpgme.m4 macro here.
dnl AM_PATH_GPGME(0.3.14)

min_gpgme_version=0.3.14
AC_PATH_PROG(GPGME_CONFIG, gpgme-config, no)
AC_MSG_CHECKING(for GPGME - version >= $min_gpgme_version)
ok=no
if test "$GPGME_CONFIG" != "no" ; then
      req_major=`echo $min_gpgme_version | \
		 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\1/'`
      req_minor=`echo $min_gpgme_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\2/'`
      req_micro=`echo $min_gpgme_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\3/'`
      gpgme_config_version=`$GPGME_CONFIG --version`
      major=`echo $gpgme_config_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\1/'`
      minor=`echo $gpgme_config_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\2/'`
      micro=`echo $gpgme_config_version | \
		 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\3/'`
fi
if test "$major" -eq "$req_major"; then
   if test "$minor" -eq "$req_minor"; then
     if test "$micro" -ge "$req_micro"; then
       ok=yes
     fi
   fi
fi

if test $ok = yes; then
  GPGME_CFLAGS=`$GPGME_CONFIG --cflags`
  GPGME_LIBS=`$GPGME_CONFIG --libs`
  AC_MSG_RESULT(yes)
else
  AC_MSG_ERROR(Seahorse $min_gpgme_version needed)
fi


SEAHORSE_CFLAGS="$SEAHORSE_CFLAGS $GPGME_CFLAGS"
SEAHORSE_LIBS="$SEAHORSE_LIBS $GPGME_LIBS"

AC_SUBST(SEAHORSE_CFLAGS)
AC_SUBST(SEAHORSE_LIBS)


dnl *******************
dnl ***** gettext *****
dnl *******************

ALL_LINGUAS="cs da de es fi fr ja nl no ru sl sv tr uk zh_CN"

GETTEXT_PACKAGE=seahorse
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Name of the gettext domain.])

AM_GLIB_GNU_GETTEXT

dnl ******************************

AC_OUTPUT([
Makefile
seahorse.spec
src/Makefile
src/dialogs/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
data/Makefile
pixmaps/Makefile
])
