dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)

AC_INIT(seahorse, 0.7.3)
AC_CONFIG_SRCDIR(src/main.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

AC_PROG_INTLTOOL

AM_PROG_LIBTOOL
AM_GCONF_SOURCE_2

PKG_CHECK_MODULES(SEAHORSE, libgnomeui-2.0 libglade-2.0
			    gtk+-2.0 >= 2.2.0 eel-2.0
			    gnome-mime-data-2.0
			    libbonobo-2.0 libbonoboui-2.0
			    bonobo-activation-2.0)

dnl **************
dnl GnuPG >= 1.2.x
dnl **************
AC_ARG_ENABLE(gpg-check, 
	AC_HELP_STRING([--disable-gpg-check], [check GPG version (default is yes)]),
	DO_CHECK=$enableval, DO_CHECK=yes)
	
if test	"$DO_CHECK" = "yes"; then
  min_gnupg_version=1.2.1
  AC_PATH_PROG(GNUPG, gpg, no)
  ok="no"
  if test "$GNUPG" != "no"; then
  	AC_MSG_CHECKING(for GnuPG >= $min_gnupg_version)
  	req_major=`echo $min_gnupg_version | \
		sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\1/'`
	req_minor=`echo $min_gnupg_version | \
		sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\2/'`
	req_micro=`echo $min_gnupg_version | \
		sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\3/'`
	gnupg_version=`$GNUPG --version | grep gpg`
	major=`echo $gnupg_version | \
		sed 's/^gpg (GnuPG) \([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\1/'`
	minor=`echo $gnupg_version | \
		sed 's/^gpg (GnuPG) \([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\2/'`
	micro=`echo $gnupg_version | \
		sed 's/^gpg (GnuPG) \([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\3/'`

	  if test "$major" -eq "$req_major"; then
	     if test "$minor" -eq "$req_minor"; then
	  	if test "$micro" -ge "$req_micro"; then
		       ok="yes"
	        fi
	     fi
	  fi
  fi
  
  if test "$ok" = "yes"; then
	AC_MSG_RESULT(yes)
  else
	AC_MSG_ERROR(GnuPG not found. Please install version $min_gnupg_version or greater)
  fi

fi


dnl **********
dnl GPGME
dnl **********

ok="no"
min_gpgme_version=0.3.14
AC_PATH_PROG(GPGME_CONFIG, gpgme-config, "failed")
if test $GPGME_CONFIG != "failed" ; then
      AC_MSG_CHECKING(for GPGME - version >= $min_gpgme_version)
      req_major=`echo $min_gpgme_version | \
		 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\1/'`
      req_minor=`echo $min_gpgme_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\2/'`
      req_micro=`echo $min_gpgme_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\)/\3/'`
      gpgme_config_version=`$GPGME_CONFIG --version`
      major=`echo $gpgme_config_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\1/'`
      minor=`echo $gpgme_config_version | \
                 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\2/'`
      micro=`echo $gpgme_config_version | \
		 sed 's/\([[0-9]]*\)\.\([[0-9]]*\)\.\([[0-9]]*\).*/\3/'`

	if test "$major" -eq "$req_major"; then
	   if test "$minor" -eq "$req_minor"; then
	     if test "$micro" -ge "$req_micro"; then
	       ok="yes"
	     fi
	   fi
	fi
fi

if test $ok = "yes"; then
  GPGME_CFLAGS=`$GPGME_CONFIG --cflags`
  GPGME_LIBS=`$GPGME_CONFIG --libs`
  AC_MSG_RESULT(yes)
else
  AC_MSG_ERROR(GPGME $min_gpgme_version needed)
fi


SEAHORSE_CFLAGS="$SEAHORSE_CFLAGS $GPGME_CFLAGS"
SEAHORSE_LIBS="$SEAHORSE_LIBS $GPGME_LIBS"

AC_SUBST(SEAHORSE_CFLAGS)
AC_SUBST(SEAHORSE_LIBS)

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl *******************
dnl ***** gettext *****
dnl *******************

ALL_LINGUAS="az ca cs da de en_GB es eu fi fr hr hu it ja ml ms nl no pl pt pt_BR ru sl sr sr@Latn sv tr uk zh_CN"

GETTEXT_PACKAGE=seahorse
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Name of the gettext domain.])

AM_GLIB_GNU_GETTEXT

dnl ******************************

AC_OUTPUT([
Makefile
seahorse.spec
libseahorse/Makefile
bonobo/Makefile
po/Makefile.in
help/Makefile
help/C/Makefile
data/Makefile
pixmaps/Makefile
src/Makefile
plugins/Makefile
plugins/nautilus/Makefile
])
